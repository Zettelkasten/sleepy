## Output
func print(Char char) { extern_func print_char(Char char); print_char(char); }
func print(Double double) { extern_func print_double(Double d); print_double(double); }
func print(Int int) { extern_func print_int(Int i); print_int(int); }
@Inline func print(Bool bool) { if bool { print('T'); } else { print('F'); } }
@Inline func print_line() { print('\n'); }
@Inline func print_line(Char|Double|Int|Bool line) { print(line); print_line(); }
extern_func flush();
@Inline func print_flush(Char|Double|Int|Bool text) { print(text); flush(); }
@Inline func print_line_flush(Char|Double|Int|Bool line) { print_line(line); flush(); }

## Allocation
extern_func allocate_double(Int size) -> DoublePtr;
extern_func allocate_char(Int size) -> CharPtr;
func deallocate(DoublePtr ptr) { extern_func deallocate_double(DoublePtr ptr); deallocate_double(ptr); }
func deallocate(CharPtr ptr) { extern_func deallocate_char(CharPtr ptr); deallocate_char(ptr); }
func load(DoublePtr ptr) -> Double { extern_func load_double(DoublePtr ptr) -> Double; return load_double(ptr); }
func load(CharPtr ptr) -> Char { extern_func load_char(CharPtr ptr) -> Char; return load_char(ptr); }
func store(DoublePtr ptr, Double value) { extern_func store_double(DoublePtr ptr, Double value); store_double(ptr, value); }
func store(CharPtr ptr, Char value) { extern_func store_char(CharPtr ptr, Char value); store_char(ptr, value); }
extern_func assert(Bool condition);
func memcpy(DoublePtr to, DoublePtr from, Int size) {
  extern_func memcpy_double(DoublePtr to, DoublePtr from, Int size);
  memcpy_double(to, from, size);
}
func memcpy(CharPtr to, CharPtr from, Int size) {
  extern_func memcpy_char(CharPtr to, CharPtr from, Int size);
  memcpy_char(to, from, size);
}

## Boolean Logic
@Inline func True() -> Bool { return 0 == 0; }
@Inline func False() -> Bool { return 0 != 0; }
@Inline func or(Bool a, Bool b) -> Bool { if a { return a; } else { return b; } }
@Inline func or(Bool a, Bool b, Bool c) -> Bool { return or(or(a, b), c); }
@Inline func and(Bool a, Bool b) -> Bool { if a { return b; } else { return False(); } }
@Inline func and(Bool a, Bool b, Bool c) -> Bool { return and(and(a, b), c); }
@Inline func not(Bool a) -> Bool { if (a) { return False(); } else { return True(); } }

## Simple Math
@Inline func min(Int a, Int b) -> Int { if a < b { return a; } else { return b; } }
@Inline func max(Int a, Int b) -> Int { if a > b { return a; } else { return b; } }

## String (note: the Str struct itself is declared internally, not here.)
# @RefType struct Str {
#   CharPtr start = allocate_char(8);
#   Int length = 0;
#   Int alloc_length = 8;
# }
func EmptyStr(Int alloc_length) -> @Mutable Str {
  return Str(allocate_char(alloc_length), 0, alloc_length);
}
func EmptyStr() -> @Mutable Str {
  return EmptyStr(8);
}
func resize(@Mutable Str str, Int new_alloc_length) {
  new_start = allocate_char(new_alloc_length);
  memcpy(new_start, str.start, str.alloc_length);
  deallocate(str.start);
  str.alloc_length = new_alloc_length;
  str.start = new_start;
}
func copy(Str str) -> @Mutable Str {
  @Mutable new = Str(allocate_char(str.alloc_length), str.length, str.alloc_length);
  memcpy(new.start, str.start, str.length);
  return new;
}
func get(Str str, Int pos) -> Char {
  return load(str.start + pos);
}
func set(@Mutable Str str, Int pos, Char char) {
  assert(and(0 <= pos, pos < str.length + 1));
  if pos >= str.alloc_length {
    resize(str, max(2 * str.alloc_length, 1));
  }
  store(str.start + pos, char);
  if pos == str.length {
    str.length += 1;
  }
}
func insert(@Mutable Str str, Char char, Int pos) {
  assert(and(0 <= pos, pos < str.length + 1));
  if str.length + 1 >= str.alloc_length {
    resize(str, max(2 * str.alloc_length, 1));
  }
  memcpy(str.start + pos + 1, str.start + pos, str.length - pos);
  set(str, pos, char);
}
func insert(@Mutable Str str, Char insert_char) { insert(str, insert_char, str.length); }
func insert(@Mutable Str str, Str insert_str, Int pos) {
  assert(and(0 <= pos, pos < str.length + 1));
  if str.length + insert_str.length >= str.alloc_length {
    resize(str, max(2 * str.alloc_length, str.length + insert_str.length));
  }
  memcpy(str.start + pos + insert_str.length, str.start + pos, str.length - pos);
  memcpy(str.start + pos, insert_str.start, insert_str.length);
  str.length += insert_str.length;
}
func insert(@Mutable Str str, Str insert_str) { insert(str, insert_str, str.length); }
func ToStr(Char from_char) -> @Mutable Str {
  @Mutable str = EmptyStr();
  insert(str, from_char);
  return str;
}
func +(Str a, Str b) -> @Mutable Str {
  @Mutable res = EmptyStr(a.length + b.length);
  memcpy(res.start, a.start, a.length);
  memcpy(res.start + a.length, b.start, b.length);
  res.length = a.length + b.length;
  return res;
}
func +(Str a, Char b) -> @Mutable Str {
  @Mutable res = copy(a);
  insert(res, b);
  return res;
}
func +(Char a, Str b) -> @Mutable Str {
  a_ = ToStr(a);
  @Mutable res = a_ + b;
  free(a_);
  return res;
}
func +(Char a, Char b) -> @Mutable Str {
  @Mutable res = ToStr(a);
  insert(res, b);
  return res;
}
func *(Str str, Int repetitions) -> @Mutable Str {
  @Mutable res = EmptyStr(str.length * repetitions);
  while repetitions > 0 {
    memcpy(res.start + res.length, str.start, str.length);
    res.length += str.length;
    repetitions -= 1;
  }
  return res;
}
func *(Char c, Int repetitions) -> @Mutable Str {
  c_ = ToStr(c);
  @Mutable res = c_ * repetitions;
  free(c_);
  return res;
}
func print(Str str) {
  pos = 0;
  while pos < str.length {
    print(get(str, pos));
    pos += 1;
  }
}
func print_line(Str str) {
  print(str); print_line();
}

## Conversions
func to_int(Double d) -> Int { extern_func double_to_int(Double d) -> Int; return double_to_int(d); }
