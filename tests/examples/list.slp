# pointer that will automatically be freed when out-of-scope
# TODO: implement that cleanup will be called once things leave scope
struct OwnedPtr[T] {
  value: Ptr[T];
}
func cleanup[T](ptr: OwnedPtr[T]) {
  free(ptr.value);
}
func new[T](val: T) -> OwnedPtr[T] {
  ptr = allocate[T](1);
  store(ptr, val);
  return OwnedPtr(ptr);
}

func cleanup[T](ptr: Ptr[T]) {
  # does nothing
}

struct List[T] {
  start: OwnedPtr[T];
  allocated_size: Long;
  size: Long;
}

func EmptyList[T]() -> List[T] {
  initial_size = 4l;
  ptr = allocate[T](initial_size);  # currently list are always allocated on the heap.
  return List[T](OwnedPtr(ptr), initial_size, 0l);
}

func maybe_resize[T](list: Ptr[List[T]], required_size: Long) {
  if load(list).allocated_size >= required_size { return; }
  load(list).allocated_size = max(required_size, load(list).allocated_size * 2l);
  old_start = load(list).start;
  load(list).start = OwnedPtr(allocate[T](load(list).allocated_size));
  memcpy(load(list).start.value, old_start.value, load(list).size);
  deallocate(old_start.value);
}

func add[T](list: Ptr[List[T]], value: T) {
  maybe_resize(list, load(list).size + 1l);
  store(load(list).start.value + load(list).size, value);
  load(list).size += 1l;
}
# TODO calling this "index" or so makes more sense. This is get + set.
func get[T](list: Ptr[List[T]], index: Long) -> Ptr[T] {
  assert(and(0l <= index, index < load(list).size));
  return load(list).start.value + index;
}
func get[T](list: Ptr[List[T]], index: Int) -> Ptr[T] { return get(list, ToLong(index)); }
func pop[T](list: Ptr[List[T]]) -> T {
  assert(load(list).size > 0l);
  value = load(list[load(list).size - 1l]);
  load(list).size -= 1l;
  return value;
}
func remove_all[T](list: Ptr[List[T]]) {
  pos = 0l;
  while pos < load(list).size {
    cleanup(list[pos]);
    pos += 1l;
  }
  load(list).size = 0l;
}
func cleanup[T](list: List[T]) {
  remove_all(ref(list));
  cleanup(list.start);
}

func print[T](list: List[T]) {
  print('[');
  pos = 0l;
  while pos < list.size {
    print(load(ref(list)[pos]));
    pos += 1l;
    if pos < list.size { print(','); print(' '); }
  }
  print(']');
}

func main() {
  a = EmptyList[Double]();
  val = 0l;
  while val < 10l {
    add(ref(a), ToDouble(val));
    val += 1l;
  }
  ref(a)[0] = -5.0;
  print_line(a);
  assert(a.size == 10l);
  print_line(pop(ref(a)));
  print_line(a);
  assert(a.size == 9l);
  remove_all(ref(a));
  assert(a.size == 0l);
  cleanup(a);
}