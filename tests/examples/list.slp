# pointer that will automatically be freed when out-of-scope
# TODO: implement that cleanup will be called once things leave scope
struct OwnedPtr[T] {
  value: Ptr[T];
}
func cleanup[T](ptr: OwnedPtr[T]) {
  free(ptr.value);
}
func cleanup(str: Str) {
  free(str);
}
func new[T](val: T) -> OwnedPtr[T] {
  ptr = allocate[T](1);
  store(ptr, val);
  return OwnedPtr(ptr);
}

func cleanup[T](ptr: Ptr[T]) {
  # does nothing
}

struct List[T] {
  start: OwnedPtr[T];
  allocated_size: Long;
  size: Long;
}

func EmptyList[T]() -> List[T] {
  initial_size = 4l;
  ptr = allocate[T](initial_size);  # currently list are always allocated on the heap.
  return List[T](OwnedPtr(ptr), initial_size, 0l);
}

func maybe_resize[T](mutates list: List[T], required_size: Long) {
  if list.allocated_size >= required_size { return; }
  list.allocated_size = max(required_size, list.allocated_size * 2l);
  old_start = list.start;
  list.start = OwnedPtr(allocate[T](list.allocated_size));
  memcpy(list.start.value, old_start.value, list.size);
  deallocate(old_start.value);
}

func add[T](mutates list: List[T], value: T) {
  maybe_resize(list, list.size + 1l);
  store(list.start.value + list.size, value);
  list.size += 1l;
}
func index[T](list: List[T], i: Long) -> Ptr[T] {
  assert(and(0l <= i, i < list.size));
  return list.start.value + i;
}
func index[T](list: List[T], i: Int) -> Ptr[T] { return index(list, ToLong(i)); }
func pop[T](mutates list: List[T]) -> T {
  assert(list.size > 0l);
  value = load(list[list.size - 1l]);
  list.size -= 1l;
  return value;
}
func remove_all[T](mutates list: List[T]) {
  pos = 0l;
  while pos < list.size {
    cleanup(list[pos]);
    pos += 1l;
  }
  list.size = 0l;
}
func cleanup[T](mutates list: List[T]) {
  remove_all(list);
  cleanup(list.start);
}

func print[T](list: List[T]) {
  print('[');
  pos = 0l;
  while pos < list.size {
    print(load(list[pos]));
    pos += 1l;
    if pos < list.size { print(','); print(' '); }
  }
  print(']');
}

func swap[T](a: Ptr[T], b: Ptr[T]) {
  tmp = load(a);
  store(a, load(b));
  store(b, tmp);
}
func quicksort[T](mutates list: List[T]) {
  _quicksort(list, list[0], list[list.size - 1l]);
}
func _quicksort[T](mutates list: List[T], from: Ptr[T], to: Ptr[T]) {
  if from >= to { return; }
  pivot = from;
  pivot_val = load(pivot);
  i = from + 1;
  j = to;
  assert(and(list[0] <= i, i <= j, j <= list[list.size - 1l]));
  # goal: [< pivot] pivot [>= pivot]
  while i < j {
    while and(i < to, load(i) < pivot_val) { i += 1l; }
    while and(j > from, load(j) >= pivot_val) { j -= 1l; }
    if i < j { swap(i, j); }
  }

  if load(j) < pivot_val { swap(j, pivot); }
  pivot = j;

  _quicksort(list, from, pivot - 1l);
  _quicksort(list, pivot + 1l, to);
}

func is_sorted[T](list: List[T]) -> Bool {
  i = 1l;
  while i < list.size {
    if load(list[i - 1l]) > load(list[i]) { return False(); }
    i += 1l;
  }
  return True();
}

func main() {
  a = EmptyList[Double]();
  val = 10l;
  while a.size < 10l {
    add(a, ToDouble(val));
    val -= 1l;
  }
  a[0] = -5.0;
  print_line(a);
  assert(a.size == 10l);
  print_line(pop(a));
  print_line(a);
  assert(a.size == 9l);
  assert(not(is_sorted(a)));
  quicksort(a);
  print_line(a);
  assert(a.size == 9l);
  assert(is_sorted(a));
  remove_all(a);
  assert(a.size == 0l);
  cleanup(a);
}